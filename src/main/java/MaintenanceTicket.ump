namespace ca.mcgill.ecse.assetplus.model;

// Authors: Émilia Gagné, Julia Grenier, Camille Pouliot, Anjali Singhal
class MaintenanceTicket {
  depend ca.mcgill.ecse.assetplus.application.AssetPlusApplication;
  // Also needs to deal with date
  depend java.sql.Date;
	
  Status {
    Open { 
      managerReviews(HotelStaff staff, PriorityLevel priority, TimeEstimate timeToResolve, boolean approvalRequired) / {
                doReview(staff, priority, timeToResolve, approvalRequired);
            } -> Assigned;
    }
    Assigned {
      startWork() -> InProgress;
    }
    InProgress {
      completeWork() [hasFixApprover()] -> Resolved;
      completeWork() [!hasFixApprover()] -> Closed;
    }
    Resolved {
      approveWork() -> Closed;
      disapproveWork(Date date, String desc) / {
      	doDisapproveWork(date, desc);
      } -> InProgress;
                         
    }
    Closed {}
  }
  
  private void doReview(HotelStaff staff, PriorityLevel priority, TimeEstimate timeToResolve, boolean approvalRequired) {
      setTicketFixer(staff);
      setPriority(priority);
      setTimeToResolve(timeToResolve);
      setFixApprover(approvalRequired ? AssetPlusApplication.getAssetPlus().getManager() : null);
  }
  
  private void doDisapproveWork(Date date, String desc) {
    MaintenanceNote newNote = addTicketNote(date, desc, AssetPlusApplication.getAssetPlus().getManager());
    addTicketNote(newNote);
  }
}